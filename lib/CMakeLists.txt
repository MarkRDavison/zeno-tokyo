FUNCTION(submodule MODULE_NAME MODULE_LIBS INCLUDE_DIRS)
	STRING(TOLOWER ${MODULE_NAME} MODULE_NAME_LOWER)
	SET(LIB_NAME "tokyo-${MODULE_NAME_LOWER}")

	SET(LIBRARY_MODULE_PATH "${PROJECT_SOURCE_DIR}/lib/${MODULE_NAME_LOWER}")
	SET(LIBRARY_SRC_PATH "${LIBRARY_MODULE_PATH}/src" )
	SET(LIBRARY_INCLUDE_PATH "${LIBRARY_MODULE_PATH}/include")

	FILE(GLOB_RECURSE LIB_HEADER_FILES "${LIBRARY_INCLUDE_PATH}/tokyo/${MODULE_NAME}/*.hpp")
	FILE(GLOB_RECURSE LIB_INLINE_FILES "${LIBRARY_INCLUDE_PATH}/tokyo/${MODULE_NAME}/*.inl")
	FILE(GLOB_RECURSE LIB_SOURCE_FILES "${LIBRARY_SRC_PATH}/*.cpp")

	SET(LIB_HEADER_FILES ${LIB_HEADER_FILES} ${LIB_INLINE_FILES})
	
	add_library (${LIB_NAME} ${LIB_TYPE} ${LIB_SOURCE_FILES} ${LIB_HEADER_FILES})
	SET_PROPERTY(TARGET ${LIB_NAME} PROPERTY CXX_STANDARD ${TOKYO_CXX_STANDARD})
	SET_PROPERTY(TARGET ${LIB_NAME} PROPERTY CXX_STANDARD_REQUIRED ON)
	TARGET_INCLUDE_DIRECTORIES(${LIB_NAME} PUBLIC ${LIBRARY_INCLUDE_PATH})
		
	if (TOKYO_STRICT)
		if (MSVC)
			# warning level 4
			target_compile_options(${LIB_NAME} PRIVATE /W4)
		else()
			# additional warnings
			target_compile_options(${LIB_NAME} PRIVATE -Wall -Wextra -Wpedantic)
		endif()
	endif()

	foreach(liblib IN LISTS MODULE_LIBS)
		TARGET_LINK_LIBRARIES(${LIB_NAME} PUBLIC ${liblib})
	endforeach()
	foreach(dir IN LISTS INCLUDE_DIRS)
		target_include_directories(${LIB_NAME} PUBLIC ${dir})
	endforeach()

	INSTALL(TARGETS ${LIB_NAME} DESTINATION ${LIB_INSTALL_FOLDER})

	IF (${BUILD_SHARED_LIBS})
		INSTALL(FILES $<TARGET_PDB_FILE:${LIB_NAME}> DESTINATION ${BIN_INSTALL_FOLDER} OPTIONAL)
	ENDIF()

	INSTALL(
		DIRECTORY "${LIBRARY_INCLUDE_PATH}/tokyo/${MODULE_NAME}/"
		DESTINATION "include/tokyo/${MODULE_NAME}")
ENDFUNCTION()

ADD_SUBDIRECTORY(core)
ADD_SUBDIRECTORY(game)
ADD_SUBDIRECTORY(driller)